#---------------------------------------------------#
#
# Blimp Script for Moderated Regression Model with 
#  Sum Scores
# 
#---------------------------------------------------#

# Read in and set up data
DATA: pain.dat;            # Read Data in
VARIABLES:                 # List Variable Names
    id txgrp male age edugroup workhrs
    exercise pain severity anxiety stress
    control depress interfere disab 
    dep1:dep7 interf1:interf6 disab1:disab6;
ORDINAL: severity male;    # Specify Binary Variables
MISSING: 999;              # Missing data code
FIXED:  male;              # Specify variables with no missing
CENTER: dep1:dep7;         # Center individual items
# Specify Regression Model
MODEL:
    # Define depression composite
    dep = dep1 + dep2 + dep3 + dep4 + dep5 + dep6 + dep7;
    # Shorthand: dep = dep1:+:dep7;
    
    focal:
        disab ~ 1@b0 dep@b1 male@b2 dep*male@b3;

    predictor:
        dep1:dep7 ~ 1 male;

# Conditional Effects Analysis (manually specify)
PARAMETERS:
    b0_female = b0;
    b1_female = b1;

    b0_male = b0 + b2;
    b1_male = b1 + b3;

# Specify the MCMC sampler
SEED: 19723;               # Set a prng seed
BURN: 5000;                # Set number of burn iterations
ITERATIONS: 10000;         # Set number of analysis iterations
CHAINS: 4;                 # Specify number independent of chains
